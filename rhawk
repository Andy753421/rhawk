#!awk -f

@include "irc.awk"
@include "spades.awk"
@include "email.awk"

# Initialization
BEGIN {
	OWNER = "andy753421"
	connect("localhost", "rhawk", "#rhtest");
	if (FIRST) {
		join("#rhnoise")
		join("#rhspades")
		join("#sfvlug")
	}
}

# Admin
FROM == OWNER && TO == NICK && /^die in a fire/ {
	say("Ack, argh, barasdjf..")
	quit()
}

FROM == OWNER && TO == NICK && /^reload/ {
	say("Reloading..")
	reload()
}

FROM == OWNER && TO == NICK && /^rejoin/ {
	reply("joining..")
	join("#rhnoise")
	next
}

FROM == OWNER && TO == NICK && /^(join|part)/ {
	match(MSG, /(join|part) +(#+\w+)/, arr)
	if (arr[1] && arr[2]) {
		send(toupper(arr[1]) " " arr[2]);
		next
	}
}

FROM == OWNER && TO == NICK && /^\.msg/ {
	match(MSG, /.*\.msg +(#*\w+) +(.*)/, arr)
	send("PRIVMSG " arr[1] " :" arr[2])
}

# Kick handling
CMD == "KICK" && ARG == NICK {
	kick_delay = (kick_delay + 2) * 2
	system("sleep " kick_delay)
	join(DST)
	reply("I feel happy!")
}

# Identify bots
FROM ~ /bo+t$|rhnoise/ {
	bots[FROM] = 1
}

CMD == "NICK" && FROM in bots {
	bots[MSG] = FROM
}


# Unicode
/[Uu]nicode :-?\(/ {
	plain[FROM] = 1
}

/[Uu]nicode :-?\)/ {
	plain[FROM] = 0
}

# SFV Lug
BEGIN { pollchan = "#sfvlug" }
(CMD == "PING"    && systime()-lastpoll > 60*60*24) ||
(CMD == "PRIVMSG" && DST == pollchan && /^\.poll/) {
	if (!topics[pollchan]) {
		debug("Unknown topic for " pollchan);
		next
	}
	_curl     = "curl -s http://sfvlug.org/"
	_day      = "(Sun|Mon|Tue|Wed|Thu|Fri|Sat)"
	_web_ptrn = "next meeting.*" _day "\\w+[, ]+([A-Z]\\w+) +([0-9]+)[, ]+([0-9]+)"
	_irc_ptrn = _day "\\w*[, ]+([A-Z]\\w+) +([0-9]+)"
	while (_curl | getline _line) {
		if (match(_line, _web_ptrn, _parts)) {
			_date  = _parts[1] " " _parts[2] " " _parts[3]
			_topic = topics[pollchan]
			sub(_irc_ptrn, _date, _topic)
			if (_topic != topics[pollchan])
				topic(pollchan, topics[pollchan] = _topic)
			else
				debug("topic is already correct")
			break
		}
	}
	lastpoll = systime()
	close(cmd)
}

# Fortune
TO == NICK && /^/               { extra = ""   }
TO == NICK && /^\.?fortune.*-o/ { extra = "-o" }
TO == NICK && /^\.?fortune/     {
	gsub(/.*\.?fortune *|-[a-z]* *|[^a-zA-Z0-9 ]/, "", MSG)
	cmd = "fortune " extra " " (MSG ? "-m '" MSG "'" : "-s")
	_lines = 0
	while (cmd | getline _fortune && _lines < 5) {
		say(_fortune)
		_lines++
	}
	close(cmd)
	next
}

# Noise
FROM ~ OWNER && /^go go gadget woop/ {
	for (i=20; i>0; i--)
		say(".delay " i " seconds; .woop " i)
}

TO == NICK && /^[Ww][Oo]+[Pp]/ {
	gsub(/[^Oo]/, "", $1)
	_len = length($1)
	if (_len == 2)
		_len = 10
	_woop = "WOOP"
	for (i=1; i<_len; i++)
		_woop = _woop " WOOP"
	say(_woop)
}

TO == NICK && DST ~ /^#/ {
	#say("Hello, " FROM)
}

!(FROM in bots) &&
MSG !~ /^\./ &&
/awk/ {
	say("Awk, awk, awk! I'm a bird!")
}

!(FROM in bots) &&
/^[^.](help|halp)/ {
	reply("Nothing can help you now..")
}

/Ho.*Ho.*Ho/ {
	say("\00309Merry \00304Christmas!")
}

{ fflush("") }

# vim: ft=awk
